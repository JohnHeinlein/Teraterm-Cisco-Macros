; .======================================
; | Cisco flash wiping macro
; |======================================
; | - Parses output of 'dir flash:'
; | - Adds non-critical files to string array
; | - Loops through array and sends delete commands
; | - TODO:
; | 	- Somehow detect length of output and size array appropriately?
; '--------------------------------------

; Cache globals
old_timeout = timeout

if debug then
	newstat = #10'[DEBUG] Creating fake dirs...'
	include stat
	include 'test\mkdir.ttl'
	newstat =  ' done!'
	include stat
endif

setsync 1
sendln 'dir flash:'

; .======================================
; | Parse Flash
; '--------------------------------------
newstat = #10'Parsing files...'
include stat

strdim files 200  ; String array of len 50
filenum = 0
delnum = 0
savenum = 0
timeout = 1

do
	recvln
	got_line = result
	
	; Deal with 'more' prompt
	; Doesn't trigger recvln since there's no EOL
	if result = 0 then
		if debug then
			newstat = #10'[DEBUG] End/More, sending space'
			include stat
		endif
		send ' '
		recvln
		got_line = result
	endif
	
	strmatch inputstr '\s{2}\S+$'
	if result > 0 then ; If a file...
		filename = matchstr
		strremove filename 1 2 ; Trim leading whitespace (Needed for regex)
		
		; Verbosity
		filenum = filenum + 1
		
		strmatch filename '((SE[0-9])|(E(X?)[0-9])|(\.bin)|(\.conf)|(\.lic)|(\.pkg)|(\.pack)$)'
		if result > 0 then
			savenum = savenum + 1
		elseif result = 0 then
			files[delnum] = filename
			delnum = delnum + 1
		endif
		
		if debug then
		; messagebox 'foo' ''
			;int2str newstat filenum
			;strconcat newstat ': '
			;strconcat newstat filename
			;include stat
		endif
	endif
loop while got_line

timeout = 0
newstat = ' done!'
include stat

; .======================================
; | Print summary
; '--------------------------------------
int2str filenum_str filenum
int2str delnum_str delnum
int2str savenum_str savenum

newstat = #10'Files found: '
strconcat newstat filenum_str
strconcat newstat #10#9'To delete: '
strconcat newstat delnum_str
strconcat newstat #10#9'To save: '
strconcat newstat savenum_str
include stat

; .======================================
; | Delete files
; '--------------------------------------

newstat = #10'Delnum check'
include db

if delnum < 1 then 
	newstat = #10'No files to delete!'
	include stat
	exit
endif

newstat = #10'Deleting files...'
include stat

for i 0 (delnum - 1)
	delstr = 'del /r /f flash:'
	;ifdefined files[i]
	;if result != 0 then
	;	strconcat delstr files[i]
	;	sendln delstr
	;endif
	
	; dynamically update deletion progress
	;if debug then ; might add to non-debug mode too
		statstr_tmp = statstr
		
		int2str i_str (i + 1)
		tmp = ' ('
		strconcat tmp i_str
		strconcat tmp '/'
		strconcat tmp delnum_str
		strconcat tmp ') '
		if debug strconcat tmp files[i]
		strconcat statstr tmp
		statusbox statstr hostname
		
		statstr = statstr_tmp
	;endif
	strconcat delstr files[i]
	sendln delstr
	
	; Doesn't seem to like the spam of not waiting, sometimes crashes and returns to master.ttl
	waitregex '^(([Ss]witch)|([rR]outer))[#>:]'
	;wait 'Router#'
next

newstat = ' done!'
include stat

if debug include dir_flash
newstat = #10'End of flash_wipe.ttl'
include db

setsync 0

; Restore globals
timeout = old_timeout

exit