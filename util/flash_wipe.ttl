; .======================================
; | Cisco flash wiping macro
; |======================================
; | - Parses output of 'dir flash:'
; | - Adds non-critical files to string array
; | - Loops through array and sends delete commands
; | - TODO:
; | 	- Somehow detect length of output and size array appropriately?
; '--------------------------------------
newstat = #10'Entering util/flash_wipe.ttl'
include db2

; Cache globals
old_timeout = timeout


if debug_mode = 2 then
	newstat = #10'[DEBUG] Creating fake dirs...'
	include stat
	include 'test\mkdir.ttl'
	newstat =  ' done!'
	include stat
endif

setsync 1
sendln 'dir flash:'

; .======================================
; | Parse Flash
; '--------------------------------------
newstat = #10'Parsing files...'
include stat

; No way I can tell to size the array prior, unless two separate 'dir flash' commands are run or arrays are reconstructed every step at O(n!)
strdim files 200  ; String array of len 200
filenum = 0
delnum = 0
savenum = 0
timeout = 1

do
	recvln
	got_line = result
	
	; Deal with 'more' prompt
	; Doesn't trigger recvln since there's no EOL
	if result = 0 then
		;newstat = #10'[DEBUG] End/More, sending space'
		;include db
		send ' '
		recvln
		got_line = result
	endif
	
	strmatch inputstr '\s{2}\S+$'
	if result > 0 then ; If a file...
		filename = matchstr
		strremove filename 1 2 ; Trim leading whitespace (Needed for regex)
		
		; Verbosity
		filenum = filenum + 1
		
		strmatch filename CRITICAL_FILES
		if result > 0 then
			savenum = savenum + 1
		elseif result = 0 then
			files[delnum] = filename
			delnum = delnum + 1
		endif
	endif
loop while got_line

timeout = 0
newstat = ' done!'
include stat

; .======================================
; | Print summary
; '--------------------------------------
int2str filenum_str filenum
int2str delnum_str delnum
int2str savenum_str savenum

newstat = #10'Files found: '
strconcat newstat filenum_str
strconcat newstat #10#9'To delete: '
strconcat newstat delnum_str
strconcat newstat #10#9'To save: '
strconcat newstat savenum_str
include stat

; .======================================
; | Delete files
; '--------------------------------------

newstat = #10'Delnum check'
include db

if delnum < 1 then 
	newstat = #10'No files to delete!'
	include stat
	exit
endif

newstat = #10'Deleting files...'
include stat

; Determine which flags the delete statement accepts
strmatch DEVICE 'asa'
if result > 0 then
	delstr = 'del /recursive /noconfirm disk0:'
else
	delstr = 'del /r /f flash:'
endif

for i 0 (delnum - 1)
	toSend = '' ; Reset command string
	
	; dynamically update deletion progress
	if stat_mode then
		statstr_tmp = statstr ; cache statstr
			
		int2str i_str (i + 1)
		tmp = #10' ('
		strconcat tmp i_str
		strconcat tmp '/'
		strconcat tmp delnum_str
		strconcat tmp ') '
	
		strconcat tmp files[i]
		
		strconcat statstr tmp
		statusbox statstr hostname
		statstr = statstr_tmp
	endif	

	strconcat toSend delstr
	strconcat delstr files[i]
	sendln toSend
	
	waitregex '^(([Ss]witch)|([rR]outer)|(ciscoasa))[#>:]'
next

newstat = ' done!'
include stat

setsync 0

; Restore globals
timeout = old_timeout

if debug_mode > 0 include dir_flash
newstat = #10'Exiting util/flash_wipe.ttl'
include db2
exit