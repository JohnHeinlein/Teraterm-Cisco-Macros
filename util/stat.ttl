; .======================================
; | Subroutine for printing to status box
; |======================================
; | Scrolls if we exceed string length
; | Called as:
; |   > newstat = 'new status line'
; |   > include stat
; | - 'newstat' is cleared after each call
; | - 'statstr' retains content for use elsewhere
; | util\debug.ttl checks debug status and prefaces 
; | newlines with [DEBUG] before calling this file.
; '--------------------------------------
ifdefined statstr
if result = 0 statstr = ''

gethostname hostname

ifdefined newstat
if result = 0 then
	;messagebox 'ERR: newstat undefined!'
	statusbox statstr hostname
	exit
endif

; .======================================
; | Cache global variables
; '--------------------------------------
ifdefined groupmatchstr1
if result tmp_groupmatchstr1 = groupmatchstr1

ifdefined groupmatchstr2 
if result tmp_groupmatchstr2 = groupmatchstr2

; .======================================
; | Remove as many lines from the top as necessary
; '--------------------------------------
strlen statstr
len_stat = result

strlen newstat
len_newstat = result
while (len_stat + len_newstat) > 510

	; Split by newlines, store into 2 variables
	; (Pop off topmost line)
	strsplit statstr #10 2
	
	; Everything but the first line
	statstr = groupmatchstr2
	strlen statstr
	len_stat = result
	
	;debug
	int2str len_stat_str len_stat
	int2str len_newstat_str len_newstat
	int2str total_len_str (len_stat + len_newstat)
	
loop ;while (len_stat + len_newstat) > 510 ;511 max len, keep space for newline

; Append newstat to current stat body and display
;strconcat statstr #10
strconcat statstr newstat
; Debug: length of statstr vs max str len
if debug then
	strlen statstr
	int2str charcount result
	strconcat hostname ' ['
	strconcat hostname charcount
	strconcat hostname '/511]'
endif
statusbox statstr hostname

; Restore global variables
ifdefined tmp_groupmatchstr1
if result groupmatchstr1 = tmp_groupmatchstr1

ifdefined tmp_groupmatchstr2
if result groupmatchstr2 = tmp_groupmatchstr2

newstat = ''
exit