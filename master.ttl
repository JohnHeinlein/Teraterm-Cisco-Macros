; .======================================
; | Driver for all macro files
; |--------------------------------------
; | Determines the type of device being run, and
; |	calls appropriate subroutines
; '--------------------------------------

; Aliases for subrouines
dir_flash = 'util\dir_flash.ttl'
stat = 'util\stat.ttl'
db = 'util\debug.ttl'

; .======================================
; | Debugging
; |--------------------------------------
; | Running files directly messes up the relative paths,
; |	so I run it from here instead of having a bunch
; |	of repeated debugging code
; '--------------------------------------
debug = 1
if debug then
	gethostname hostname
	strconcat hostname ' [DEBUG]'
	newstat = 'init'
	include db
	
	; File to test
	totest = ''
	
	strlen totest
	if result > 0 then
		include totest
		end
	endif
endif

; .======================================
; | init
; '--------------------------------------
statstr = ''
gethostname hostname
statusbox statstr hostname

; .======================================
; | Model selector (DIY switch statement)
; |--------------------------------------
; | Once in ROMMON, we use some heuristic to determine the model of switch.
; | Not trivial, since different switches can have dramatically different 
; | bootloaders and rom monitors
; '--------------------------------------
setsync 1
sendln 'version'
recvln ; First captures the command i just sent? lol
recvln
setsync 0
if result = 1 then
	; Catalyst 2960 Series Switches
	strmatch inputstr '^C2960.?\s'	
	if result = 1 then
		newstat = '**C2960 ROMMON Detected**'
		include stat
		include 'rommon\2960.ttl'
		include 'os\switch.ttl'
		goto finish
	endif
	
	; Catalyst 3000 Series Switches
	strmatch inputstr '^CAT3K_CAA\s'
	if result = 1 then
		newstat = '**C3K ROMMON Detected**'
		include stat
		include 'rommon\cat3k.ttl'
		include 'os\cat3k.ttl'
		goto finish
	endif
	
	strmatch inputstr '^C3750[A-Z]?\s'
	if result = 1 then
		newstat = '**C3750 ROMMON Detected**'
		include stat
		include 'rommon\2960.ttl'
		include 'os\switch.ttl'
		goto finish
	endif
	
	; If version fails, it should be a router.
	; DEFINITELY subject to change, pending more strange devices.
	strmatch inputstr '(\nmonitor: )?command \"version\" not found'
	if result > 0 then
		:router
		setsync 1
		sendln ''
		recvln
		setsync 0
		
		strmatch inputstr 'rommon [0-9]+'
		if result = 1 then
			newstat = 'Router assumed'
			include stat
			include 'rommon\isr.ttl'
			include 'os\router.ttl'
			goto finish
		endif
	endif
	if result = 0 goto no_match
endif


:finish
if debug != 1then
	clearscreen 1
	include dir_flash
endif

gethostname hostname
endMessage = #10#10
strconcat endMessage hostname
strconcat endMessage ' finished!'
strconcat statstr endMessage
messagebox statstr 'Done!'

end

:no_match
messagebox 'No supported device detected, exiting' 'Err'
end
