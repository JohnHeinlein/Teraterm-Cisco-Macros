; .======================================
; | Driver for all macro files
; |--------------------------------------
; | Determines the type of device being run, and
; |	calls appropriate subroutines
; '--------------------------------------

; Aliases for subrouines
dir_flash = 'util\dir_flash.ttl'
stat = 'util\stat.ttl'
db = 'util\debug.ttl'
db2 = 'util\debug2.ttl' ; Wrapper to check for debug level 2

; .======================================
; | Debugging
; |--------------------------------------
; | Running files directly messes up the relative paths.
; | Set 'totest' to path & name of file to test
; | debug = 1 -> Some extra logging
; | debug = 2 -> Log entry & exit of subroutines
; '--------------------------------------
debug = 1
totest = '' ; If none is specified, 'debug = 1' just enables debug messages.
if debug > 0 then
	gethostname hostname
	strconcat hostname ' [DEBUG]'
	newstat = 'init'
	include db
	
	strlen totest
	if result > 0 then
		include totest
		end
	endif
endif

; .======================================
; | init
; '--------------------------------------
statstr = ''
newstat = ''
gethostname hostname
statusbox statstr hostname

newstat = hostname
strconcat newstat ' init'
include stat

; .======================================
; | Model selector (DIY switch statement)
; |--------------------------------------
; | Once in ROMMON, we use some heuristic to determine the model of switch.
; | Not trivial, since different switches can have dramatically different 
; | bootloaders and rom monitors
; '--------------------------------------
setsync 1
sendln 'version'
recvln ; First captures the command i just sent? lol
recvln
setsync 0
if result = 1 then
	; Catalyst 2960 Series Switches
	strmatch inputstr '^C2960.?\s'	
	if result = 1 then
		newstat = '**C2960 ROMMON Detected**'
		include stat
		include 'rommon\2960.ttl'
		include 'os\switch.ttl'
		goto finish
	endif
	
	; Catalyst 3000 Series Switches
	strmatch inputstr '^CAT3K_CAA\s'
	if result = 1 then
		newstat = '**C3K ROMMON Detected**'
		include stat
		include 'rommon\cat3k.ttl'
		include 'os\cat3k.ttl'
		goto finish
	endif
	
	strmatch inputstr '^C3750[A-Z]?\s'
	if result = 1 then
		newstat = '**C3750 ROMMON Detected**'
		include stat
		include 'rommon\2960.ttl'
		include 'os\switch.ttl'
		goto finish
	endif
	
	; If version fails, it should be a router.
	; Some "routers" will print a blank line before the actual error. This block consumes that.
	strmatch inputstr ''
	if result > 0 then
		recvln
	endif

	strmatch inputstr 'command \"version\" not found'
	if result > 0 then
		setsync 1
		sendln ''
		recvln
		setsync 0
		
		strmatch inputstr 'rommon [0-9]+'
		if result > 0 then
			newstat = #10'Router assumed'
			include stat
			include 'rommon\isr.ttl'
			include 'os\router.ttl'
			goto finish
		endif
		goto no_match
	endif
	
	
	if result = 0 goto no_match
endif


:finish
if debug < 1 then
	clearscreen 1
	include dir_flash
endif

gethostname hostname
endMessage = #10#10
strconcat endMessage hostname
strconcat endMessage ' finished!'
strconcat statstr endMessage
messagebox statstr 'Done!'

end

:no_match
messagebox 'No supported device detected, exiting' 'Err'
end
