; =========================
; Initialization
; =========================
; Debug -----
	; Initialize variables.
	debug = 0
	if debug = 1 then
		statstr = 'foo'
		gethostname hostname
		dir_flash = '..\util\dir_flash.ttl'
	else
		dir_flash = 'util\dir_flash.ttl'
	endif
; ----- ----- -----

; Verbosity -----
	strconcat statstr #10'Booting ROUTER...'
	statusbox statstr hostname
; ----- ----- -----
timeout = 0

; =========================
; Wait for "Press RETURN" prompt
; =========================
wait 'Press RETURN'
send #13 ;ASCII CR

; Verbosity -----
	strconcat stastr ' done!'
	statusbox statstr hostname
; ----- ----- -----

wait 'Router>'
sendln 'enable'
wait 'Router#'

; =========================
; Wait for stdout to finish spamming after boot.
; =========================
; Verbosity  -----
	strconcat statstr #10'Waiting for std out to quiet down...'
	statusbox statstr hostname
; ----- ----- -----
if debug = 1 then
	timeout = 1
else
	timeout = 10
endif

do
	recvln
loop while result = 1

; =========================
; Parse flash for files
; =========================
include 'util\flash_parse.ttl'

; =========================
; Generate & execute delete commands
; =========================
include 'util\flash_wipe.ttl'

include dir_flash

; =========================
; Wipe NVRAM
; =========================
strconcat statstr #10'Clearing NVRAM...'
statusbox statstr hostname

wait 'Switch#'
sendln 'write erase'
wait '[confirm]'
send #13
wait 'Initialized the geometry of nvram'
pause 1

strconcat statstr ' done!'
statusbox statstr hostname

; =========================
; Reboot and disable confreg
; =========================
:finish

if debug = 1 then
	exit ; Don't reboot
endif

strconcat statstr #10'Rebooting to reset confreg..'
statusbox statstr hostname

sendln 'reload'
wait '[confirm]'
send #13

timeout = 0
wait '#####' ;Boot spams octothorpes; just capture a few
sendbreak ; Re-enter ROMMON
;pause 1
sendbreak ; Second break just because it might be a little spicy
wait 'rommon'
sendln 'confreg 0x2102'

messagebox 'Done' 'Completed'