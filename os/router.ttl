; .======================================
; | Cisco Router wiping macro
; |======================================
; | - Wait for boot
; | - Wait for stdout to clear once the cmd loads (console spam)
; | - Call subroutines to generate and execute wipe commands dynamically
; | - Clear NVRAM to prompt initial setup dialog on next boot
; | - Reboot to restore confreg in ROMMON
; '--------------------------------------
newstat = #10'Entering os/router.ttl'
include db2

wait 'Router>'
sendln 'enable'
wait 'Router#'

; Disable logging to console while we're working.
; Too much of a pain to worry about std out being spammed
if debug then
	newstat =  #10'[DEBUG] Configuring terminal logging...'
	include stat
endif
sendln 'configure terminal'
wait 'Router(config)#'
sendln 'no logging console'
wait 'Router(config)#'
sendln 'exit'
if debug then
	newstat = ' done!'
	include stat
endif

; .======================================
; | Wait for stdout to finish spamming after boot.
; '--------------------------------------
; Might not be necessary anymore, since we disable console logging above
;include 'util\stdout.ttl'

; .======================================
; | Wipe flash
; '--------------------------------------
include 'util\flash_wipe.ttl'

; .======================================
; | Wipe NVRAM
; '--------------------------------------
newstat = #10'Clearing NVRAM...'
include stat

send #13
wait 'Router#'
sendln 'write erase'
wait '[confirm]'
send #13
waitregex 'Initialized the geometry of nvram' 'Erase of nvram: complete'
pause 1

newstat = ' done!'
include stat

; .======================================
; | Re-enable console logging
; '--------------------------------------
wait 'Router#'
sendln 'configure terminal'
wait 'Router(config)#'
sendln 'logging console 5'
wait 'Router(config)#'
sendln 'exit'

; .======================================
; | Reboot and disable confreg
; '--------------------------------------

newstat = #10'Rebooting to reset confreg...'
include stat

sendln 'reload'
waitregex '\[confirm\]' '\[yes/no\]'
if result = 2 then
	sendln 'no'
	wait '[confirm]'
endif
send #13

timeout = 0
wait '#####' ;Boot spams octothorpes; just capture a few
sendbreak ; Re-enter ROMMON
;pause 1
sendbreak ; Second break just because it might be a little spicy
wait 'rommon'
sendln 'confreg 0x2102'

include dir_flash

newstat = ' done!'
include stat

newstat = #10'Exiting os/router.ttl'
include db2
exit
