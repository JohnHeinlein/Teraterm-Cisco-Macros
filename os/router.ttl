; .======================================
; | Cisco Router wiping macro
; |======================================
; | - Wait for boot
; | - Wait for stdout to clear once the cmd loads (console spam)
; | - Call subroutines to generate and execute wipe commands dynamically
; | - Clear NVRAM to prompt initial setup dialog on next boot
; | - Reboot to restore confreg in ROMMON
; '--------------------------------------
newstat = #10'Entering os/router.ttl'
include db2

wait 'Router>'
sendln 'enable'
wait 'Router#'

; .======================================
; | Disable console logging
; '--------------------------------------
newstat =  #10'Configuring terminal logging...'
include db

sendln 'configure terminal'
wait 'Router(config)#'
sendln 'no logging console'
wait 'Router(config)#'
sendln 'exit'

newstat = ' done!'
include db

; Might not be necessary anymore, since we disable console logging now. Left in just in case
;include 'util\stdout.ttl'

; .======================================
; | Wipe flash
; '--------------------------------------
include 'util\flash_wipe.ttl'

; .======================================
; | Wipe NVRAM
; '--------------------------------------
newstat = #10'Clearing NVRAM...'
include stat

send #13
wait 'Router#'
sendln 'write erase'
wait '[confirm]'
send #13
waitregex 'Initialized the geometry of nvram' 'Erase of nvram: complete'
pause 1

newstat = ' done!'
include stat

; .======================================
; | Re-enable console logging
; '--------------------------------------
newstat = #10'Resetting logging & confreg...'
include stat

wait 'Router#'
sendln 'configure terminal'
wait 'Router(config)#'
sendln 'logging console 5'
wait 'Router(config)#'
sendln 'config-register 0x2102'
wait 'Router(config)#'
sendln 'exit'

include dir_flash

newstat = ' done!'
include stat

newstat = #10'Exiting os/router.ttl'
include db2
exit
